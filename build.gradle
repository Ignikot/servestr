buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.1.1'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

version = '3.0.0-SNAPSHOT'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    compile 'io.vertx:vertx-core:3.0.0-SNAPSHOT'
    compile 'io.vertx:vertx-web:3.0.0-SNAPSHOT'
    compile 'io.vertx:vertx-auth-common:3.0.0-SNAPSHOT'
    compile 'io.vertx:vertx-auth-shiro:3.0.0-SNAPSHOT'
    compile 'io.vertx:vertx-auth-service:3.0.0-SNAPSHOT'
    compile 'io.vertx:vertx-service-factory:3.0.0-SNAPSHOT'
    compile 'io.vertx:vertx-mongo-client:3.0.0-SNAPSHOT'
    compile 'io.vertx:vertx-codegen:3.0.0-SNAPSHOT'
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Starter'
        attributes 'Main-Verticle': 'com.uchenikimira.servestr.vertx.MainVerticle'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task start(dependsOn: shadowJar) << {
    javaexec {
        main = "-jar"
        args = [shadowJar.archivePath, "-conf", "conf.json"]
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
